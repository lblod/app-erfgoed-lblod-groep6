PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mu:<http://mu.semte.ch/vocabularies/core/>
PREFIX ext:<http://mu.semte.ch/vocabularies/ext/>
PREFIX bestuurseenheidscode: <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/>
PREFIX dct: <http://purl.org/dc/terms/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid foaf:member ?persoon.
    ?persoon a foaf:Person;
            mu:uuid ?uuidPersoon;
            foaf:firstName "Mock";
            foaf:familyName ?naam;
            foaf:account ?account.
    ?account a foaf:OnlineAccount;
            mu:uuid ?uuidAccount;
            foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>.
    <http://lblod.data.gift/groups/admins>
            a foaf:Group ;
            foaf:member ?persoon .
  }
}
WHERE {
     ?bestuurseenheid a besluit:Bestuurseenheid;
     mu:uuid ?id;
     skos:prefLabel ?naam.
     BIND(MD5(?naam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?naam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)
}
